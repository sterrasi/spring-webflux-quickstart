buildscript {
	dependencies {
		classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.50.0'
	}
}

plugins {
	id 'java'
	id 'application'
	id 'idea'
	id 'org.springframework.boot' version "${springboot_version}"
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.diffplug.spotless' version "6.25.0"
}

group = 'com.github.sterrasi'
version = (findProperty('version') == 'unspecified') ? '0.0.1-SNAPSHOT' : version
sourceCompatibility = JavaVersion.VERSION_21;
targetCompatibility = JavaVersion.VERSION_21;

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}
application {
	applicationDefaultJvmArgs = [ '-XX:InitialRAMPercentage=75.0', '-XX:MaxRAMPercentage=90.0']
	mainClass = 'com.github.sterrasi.webflux.starter.WebfluxStarterApplication'
}

idea {
	module {
		downloadJavadoc = true
		downloadSources = true
	}
}

configurations {
	// create a mockito agent configuration
	mockitoAgent
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	gradlePluginPortal()
	mavenCentral()
}

dependencies {

	// spring boot
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis-reactive'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	// misc
	implementation "com.fasterxml.jackson.core:jackson-databind"
	implementation libs.commons.lang3

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// mapstruct
	implementation "org.mapstruct:mapstruct:${mapstruct_version}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"
	annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
	testAnnotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
	testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstruct_version}"

	// database
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'io.micrometer:micrometer-registry-dynatrace'
	runtimeOnly 'io.r2dbc:r2dbc-h2'

	// for validation
	implementation 'org.hibernate:hibernate-validator:8.0.2.Final'

	// liquibase
	implementation 'org.liquibase:liquibase-core'

	// test
	testImplementation libs.bundles.mockito
	mockitoAgent(libs.mockito.core) { transitive = false }
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


subprojects {
	spotless {

		format 'misc', {
			// define the files to apply `misc` to
			target '*.gradle', '.gitattributes', '.gitignore'

			// define the steps to apply to those files
			trimTrailingWhitespace()
			leadingSpacesToTabs() // or leadingTabsToSpaces. Takes an integer argument if you don't like 4
			endWithNewline()
		}
		java {
			// don't need to set target, it is inferred from java

			// apply a specific flavor of google-java-format
			palantirJavaFormat('2.50.0')
			// fix formatting of type annotations
			formatAnnotations()
			// make sure every file has the following copyright header.
			// optionally, Spotless can set copyright years by digging
			// through git history (see "license" section below)
			licenseHeader '/* (C)$YEAR */'
		}
	}
}

compileJava.dependsOn spotlessApply

test {
	useJUnitPlatform()
	jvmArgs=applicationDefaultJvmArgs
}

tasks {
	test {
		useJUnitPlatform()
		jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
	}
}

// compiler args
tasks.withType(JavaCompile) {
	options.compilerArgs = [
	        '-parameters',
			'-Amapstruct.suppressGeneratorTimestamp=true',
			'-Amapstruct.defaultComponentModel=spring',
			'-Amapstruct.unmappedTargetPolicy=IGNORE',
			'-Amapstruct.verbose=true',
	]
}